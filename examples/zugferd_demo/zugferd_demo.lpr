/// ZUGFeRD / Factur-X Demo - mORMot2 PDF Cross-Platform
// Reads the invoice data from factur-x.xml, draws the invoice with TGDIPages
// and embeds the same file into the PDF/A-3 it exports: a tagged hybrid
// invoice of the profile EN 16931, for Germany (ZUGFeRD) and France
// (Factur-X). Invoices to German authorities take pure XML, not a PDF.
//
// Worth noting:
// - factur-x.xml is a sample invoice of XRechnung for Delphi, contributed
//   under the licence of this project. The page is drawn from what
//   ReadInvoice finds in it, so the page and the embedded data cannot differ
// - ReadInvoice is a demo reader, not an XML parser: fixed paths and
//   prefixes, no entities, no validation. TInvoice is where the data of your
//   own application - a database, an ERP export - would go in instead
// - the labels are ASCII; every umlaut on the page comes from the UTF-8 XML,
//   so this source builds unchanged with FPC, Delphi 7 and Delphi 2010
// - ExportPdfLevel, ExportPdfTagged and the font mode are set before the first
//   NewPage: the font flags decide which metrics the layout is measured with
// - AddExportPdfAttachment and ExportPdfMetadataExtension give the export the
//   associated file and the fx: XMP properties ZUGFeRD / Factur-X require
//
// Switches, to tell the sources of a checker failure apart:
//   --no-attachment   leave factur-x.xml out (the page is still read from it)
//   --untagged        no structure tree (PDF/A without PDF/UA)
program zugferd_demo;

{$I mormot.defines.inc}
{$APPTYPE CONSOLE}

uses
  {$ifdef FPC}
  Interfaces,   // registers the widgetset (Win32 on Windows, GTK2/Cocoa on Unix)
  {$endif FPC}
  SysUtils,
  Classes,
  Graphics,
  mormot.core.base,
  mormot.core.os,
  mormot.core.unicode,
  mormot.ui.report;

const
  XML_NAME = 'factur-x.xml';
  PROFILE = 'EN 16931';

type
  /// one invoice line, the values as the XML holds them
  TInvoiceItem = record
    Name, SellerId, Description, ClassCode, Note, OrderLine: RawUtf8;
    PeriodStart, PeriodEnd: RawUtf8;
    Quantity, Price, VatRate, Total: RawUtf8;
  end;

  /// the VAT of one rate
  TInvoiceTax = record
    Basis, Rate, Amount: RawUtf8;
  end;

  /// the invoice as the page shows it - amounts with a decimal point, dates
  // as yyyymmdd, formatted only when drawn
  TInvoice = record
    Number, IssueDate, Note, BuyerReference: RawUtf8;
    PeriodStart, PeriodEnd: RawUtf8;
    SellerName, SellerTradingName, SellerDescription: RawUtf8;
    SellerVatId, SellerTaxNumber: RawUtf8;
    SellerStreet, SellerPostcode, SellerCity, SellerCountry: RawUtf8;
    SellerContact, SellerPhone, SellerEmail: RawUtf8;
    BuyerId, BuyerName, BuyerEmail: RawUtf8;
    BuyerStreet, BuyerPostcode, BuyerCity, BuyerCountry: RawUtf8;
    Currency, PaymentTerms, DueDate, PaymentReference: RawUtf8;
    Ibans, AccountNames: array of RawUtf8;
    NetTotal, GrandTotal: RawUtf8;
    Taxes: array of TInvoiceTax;
    Items: array of TInvoiceItem;
  end;

var
  WithAttachment, WithTags: boolean;
  Xml: RawUtf8;
  Invoice: TInvoice;
  i: integer;

{ ---------- reading factur-x.xml ---------- }

// the content of the next <Tag>...</Tag> from From on, which then points
// behind it; '' when there is none. <Tag/> and <TagMore> do not match
function NextElement(const Xml, Tag: RawUtf8; var From: PtrInt): RawUtf8;
var
  p, q, e: PtrInt;
begin
  result := '';
  p := From - 1;
  repeat
    p := PosEx('<' + Tag, Xml, p + 1);
    if p = 0 then
    begin
      From := length(Xml) + 1;
      exit;
    end;
    q := p + length(Tag) + 1;
  until (q <= length(Xml)) and (Xml[q] in ['>', ' ']);
  q := PosEx('>', Xml, q);
  e := PosEx('</' + Tag + '>', Xml, q);
  if (q = 0) or (e = 0) then
  begin
    From := length(Xml) + 1;
    exit;
  end;
  result := copy(Xml, q + 1, e - q - 1);
  From := e + length(Tag) + 3;
end;

// the text at the end of a path of nested elements, '' when one is missing
function XmlText(const Xml: RawUtf8; const Path: array of RawUtf8): RawUtf8;
var
  n: integer;
  From: PtrInt;
begin
  result := Xml;
  for n := 0 to high(Path) do
  begin
    From := 1;
    result := NextElement(result, Path[n], From);
  end;
  result := TrimU(result);
end;

// "a, b, c" from the parts that are not empty
function Join(const Parts: array of RawUtf8): RawUtf8;
var
  n: integer;
begin
  result := '';
  for n := 0 to high(Parts) do
    if Parts[n] <> '' then
      if result = '' then
        result := Parts[n]
      else
        result := result + ', ' + Parts[n];
end;

// Xml up to the first <Tag: XmlText searches all descendants, so a child is
// looked for only in the part before the elements that hold the same name
function Before(const Xml, Tag: RawUtf8): RawUtf8;
var
  p: PtrInt;
begin
  p := PosEx('<' + Tag, Xml);
  if p = 0 then
    result := Xml
  else
    result := copy(Xml, 1, p - 1);
end;

// the text of the <ram:ID> whose schemeID is Scheme, '' when there is none
function SchemeId(const Xml, Scheme: RawUtf8): RawUtf8;
var
  p, q, e: PtrInt;
  Tag: RawUtf8;
begin
  result := '';
  p := PosEx('<ram:ID ', Xml);
  while p > 0 do
  begin
    q := PosEx('>', Xml, p);
    if q = 0 then
      exit;
    Tag := StringReplaceAll(copy(Xml, p, q - p), [' ', '', '''', '"']);
    if PosEx('schemeID="' + Scheme + '"', Tag) > 0 then
    begin
      e := PosEx('</ram:ID>', Xml, q);
      if e > q then
        result := TrimU(copy(Xml, q + 1, e - q - 1));
      exit;
    end;
    p := PosEx('<ram:ID ', Xml, q);
  end;
end;

// LineOne, LineTwo and LineThree of a PostalTradeAddress, joined
function AddressLines(const Party: RawUtf8): RawUtf8;
var
  Lines: array[0..2] of RawUtf8;
begin
  Lines[0] := XmlText(Party, ['ram:PostalTradeAddress', 'ram:LineOne']);
  Lines[1] := XmlText(Party, ['ram:PostalTradeAddress', 'ram:LineTwo']);
  Lines[2] := XmlText(Party, ['ram:PostalTradeAddress', 'ram:LineThree']);
  result := Join(Lines);
end;

function ReadItem(const Line: RawUtf8): TInvoiceItem;
var
  Product: RawUtf8;
begin
  // the characteristics after them hold a ram:Description of their own
  Product := Before(XmlText(Line, ['ram:SpecifiedTradeProduct']),
    'ram:ApplicableProductCharacteristic');
  result.Name := XmlText(Product, ['ram:Name']);
  result.SellerId := XmlText(Product, ['ram:SellerAssignedID']);
  result.Description := StringReplaceAll(XmlText(Product, ['ram:Description']),
    [#13, '', #10, ', ']);
  result.ClassCode := XmlText(Line, ['ram:SpecifiedTradeProduct',
    'ram:DesignatedProductClassification', 'ram:ClassCode']);
  result.Note := XmlText(Line, ['ram:AssociatedDocumentLineDocument',
    'ram:IncludedNote', 'ram:Content']);
  result.OrderLine := XmlText(Line, ['ram:SpecifiedLineTradeAgreement',
    'ram:BuyerOrderReferencedDocument', 'ram:LineID']);
  result.Price := XmlText(Line, ['ram:SpecifiedLineTradeAgreement',
    'ram:NetPriceProductTradePrice', 'ram:ChargeAmount']);
  result.Quantity := XmlText(Line, ['ram:SpecifiedLineTradeDelivery',
    'ram:BilledQuantity']);
  result.VatRate := XmlText(Line, ['ram:SpecifiedLineTradeSettlement',
    'ram:ApplicableTradeTax', 'ram:RateApplicablePercent']);
  result.PeriodStart := XmlText(Line, ['ram:SpecifiedLineTradeSettlement',
    'ram:BillingSpecifiedPeriod', 'ram:StartDateTime', 'udt:DateTimeString']);
  result.PeriodEnd := XmlText(Line, ['ram:SpecifiedLineTradeSettlement',
    'ram:BillingSpecifiedPeriod', 'ram:EndDateTime', 'udt:DateTimeString']);
  result.Total := XmlText(Line, ['ram:SpecifiedLineTradeSettlement',
    'ram:SpecifiedTradeSettlementLineMonetarySummation', 'ram:LineTotalAmount']);
end;

// fills TInvoice from a CII invoice of the profile EN 16931 - only the fields
// this page shows
function ReadInvoice(const Xml: RawUtf8): TInvoice;
var
  Doc, Trade, Agreement, Seller, Buyer, Settlement, Line, Tax, Means: RawUtf8;
  From: PtrInt;
  n: integer;
begin
  Doc := XmlText(Xml, ['rsm:CrossIndustryInvoice', 'rsm:ExchangedDocument']);
  result.Number := XmlText(Doc, ['ram:ID']);
  result.IssueDate := XmlText(Doc, ['ram:IssueDateTime', 'udt:DateTimeString']);
  result.Note := XmlText(Doc, ['ram:IncludedNote', 'ram:Content']);
  Trade := XmlText(Xml, ['rsm:CrossIndustryInvoice',
    'rsm:SupplyChainTradeTransaction']);
  // the parties
  Agreement := XmlText(Trade, ['ram:ApplicableHeaderTradeAgreement']);
  result.BuyerReference := XmlText(Agreement, ['ram:BuyerReference']);
  Seller := XmlText(Agreement, ['ram:SellerTradeParty']);
  result.SellerName := XmlText(Seller, ['ram:Name']);
  result.SellerTradingName := XmlText(Seller, ['ram:SpecifiedLegalOrganization',
    'ram:TradingBusinessName']);
  result.SellerDescription := XmlText(Seller, ['ram:Description']);
  result.SellerVatId := SchemeId(Seller, 'VA');
  result.SellerTaxNumber := SchemeId(Seller, 'FC');
  result.SellerStreet := AddressLines(Seller);
  result.SellerPostcode := XmlText(Seller, ['ram:PostalTradeAddress',
    'ram:PostcodeCode']);
  result.SellerCity := XmlText(Seller, ['ram:PostalTradeAddress', 'ram:CityName']);
  result.SellerCountry := XmlText(Seller, ['ram:PostalTradeAddress',
    'ram:CountryID']);
  result.SellerContact := XmlText(Seller, ['ram:DefinedTradeContact',
    'ram:PersonName']);
  result.SellerPhone := XmlText(Seller, ['ram:DefinedTradeContact',
    'ram:TelephoneUniversalCommunication', 'ram:CompleteNumber']);
  result.SellerEmail := XmlText(Seller, ['ram:DefinedTradeContact',
    'ram:EmailURIUniversalCommunication', 'ram:URIID']);
  Buyer := XmlText(Agreement, ['ram:BuyerTradeParty']);
  // the party's own ID precedes its name; a later ram:ID is a registration
  result.BuyerId := XmlText(copy(Buyer, 1, PosEx('<ram:Name', Buyer)), ['ram:ID']);
  result.BuyerName := XmlText(Buyer, ['ram:Name']);
  result.BuyerStreet := AddressLines(Buyer);
  result.BuyerPostcode := XmlText(Buyer, ['ram:PostalTradeAddress',
    'ram:PostcodeCode']);
  result.BuyerCity := XmlText(Buyer, ['ram:PostalTradeAddress', 'ram:CityName']);
  result.BuyerCountry := XmlText(Buyer, ['ram:PostalTradeAddress', 'ram:CountryID']);
  result.BuyerEmail := XmlText(Buyer, ['ram:URIUniversalCommunication',
    'ram:URIID']);
  // payment and totals
  Settlement := XmlText(Trade, ['ram:ApplicableHeaderTradeSettlement']);
  result.Currency := XmlText(Settlement, ['ram:InvoiceCurrencyCode']);
  result.PaymentReference := XmlText(Settlement, ['ram:PaymentReference']);
  result.PaymentTerms := XmlText(Settlement, ['ram:SpecifiedTradePaymentTerms',
    'ram:Description']);
  result.DueDate := XmlText(Settlement, ['ram:SpecifiedTradePaymentTerms',
    'ram:DueDateDateTime', 'udt:DateTimeString']);
  result.PeriodStart := XmlText(Settlement, ['ram:BillingSpecifiedPeriod',
    'ram:StartDateTime', 'udt:DateTimeString']);
  result.PeriodEnd := XmlText(Settlement, ['ram:BillingSpecifiedPeriod',
    'ram:EndDateTime', 'udt:DateTimeString']);
  // one account per means of payment, one breakdown per VAT rate
  result.Ibans := nil;
  result.AccountNames := nil;
  n := 0;
  From := 1;
  repeat
    Means := NextElement(Settlement, 'ram:SpecifiedTradeSettlementPaymentMeans',
      From);
    if Means = '' then
      break;
    SetLength(result.Ibans, n + 1);
    SetLength(result.AccountNames, n + 1);
    result.Ibans[n] := XmlText(Means, ['ram:PayeePartyCreditorFinancialAccount',
      'ram:IBANID']);
    result.AccountNames[n] := XmlText(Means,
      ['ram:PayeePartyCreditorFinancialAccount', 'ram:AccountName']);
    inc(n);
  until false;
  result.Taxes := nil;
  n := 0;
  From := 1;
  repeat
    Tax := NextElement(Settlement, 'ram:ApplicableTradeTax', From);
    if Tax = '' then
      break;
    SetLength(result.Taxes, n + 1);
    result.Taxes[n].Basis := XmlText(Tax, ['ram:BasisAmount']);
    result.Taxes[n].Rate := XmlText(Tax, ['ram:RateApplicablePercent']);
    result.Taxes[n].Amount := XmlText(Tax, ['ram:CalculatedAmount']);
    inc(n);
  until false;
  result.NetTotal := XmlText(Settlement,
    ['ram:SpecifiedTradeSettlementHeaderMonetarySummation', 'ram:LineTotalAmount']);
  result.GrandTotal := XmlText(Settlement,
    ['ram:SpecifiedTradeSettlementHeaderMonetarySummation', 'ram:GrandTotalAmount']);
  // the lines
  result.Items := nil;
  n := 0;
  From := 1;
  repeat
    Line := NextElement(Trade, 'ram:IncludedSupplyChainTradeLineItem', From);
    if Line = '' then
      break;
    SetLength(result.Items, n + 1);
    result.Items[n] := ReadItem(Line);
    inc(n);
  until false;
end;

{ ---------- formatting for a German invoice ---------- }

// 336.9 -> 336,90 and 1234.5 -> 1.234,50
function Amount(const Value: RawUtf8): RawUtf8;
var
  Sign, Int, Frac: RawUtf8;
  p: PtrInt;
  n: integer;
begin
  Int := Value;
  Sign := '';
  if (Int <> '') and (Int[1] = '-') then
  begin
    Sign := '-';
    delete(Int, 1, 1);
  end;
  Frac := '';
  p := PosEx('.', Int);
  if p > 0 then
  begin
    Frac := copy(Int, p + 1, maxInt);
    Int := copy(Int, 1, p - 1);
  end;
  Frac := copy(Frac + '00', 1, 2);
  result := '';
  n := length(Int);
  while n > 3 do
  begin
    result := '.' + copy(Int, n - 2, 3) + result;
    dec(n, 3);
  end;
  result := Sign + copy(Int, 1, n) + result + ',' + Frac;
end;

// a quantity or a rate: 1 -> 1, 2.5000 -> 2,5, 19.00 -> 19
function Decimal(const Value: RawUtf8): RawUtf8;
begin
  result := Value;
  if (result <> '') and (result[1] = '.') then
    result := '0' + result // .5 -> 0.5
  else if (length(result) > 1) and (result[1] in ['-', '+']) and
          (result[2] = '.') then
    insert('0', result, 2); // -.5 -> -0.5
  if PosEx('.', result) > 0 then
  begin
    while (result <> '') and (result[length(result)] = '0') do
      SetLength(result, length(result) - 1);
    if (result <> '') and (result[length(result)] = '.') then
      SetLength(result, length(result) - 1);
  end;
  result := StringReplaceAll(result, '.', ',');
end;

// 20160404 -> 04.04.2016
function GermanDate(const Value: RawUtf8): RawUtf8;
begin
  if length(Value) = 8 then
    result := copy(Value, 7, 2) + '.' + copy(Value, 5, 2) + '.' +
      copy(Value, 1, 4)
  else
    result := Value;
end;

// DE79000000001234567890 -> DE79 0000 0000 1234 5678 90
function IbanGroups(const Iban: RawUtf8): RawUtf8;
var
  n: integer;
begin
  result := '';
  for n := 1 to length(Iban) do
  begin
    if (n > 1) and ((n - 1) mod 4 = 0) then
      result := result + ' ';
    result := result + copy(Iban, n, 1); // Iban[n], a char, would convert on Unicode Delphi
  end;
end;

// "a bis b", "ab a" or "bis b" - '' when neither date is there
function Period(const StartDate, EndDate: RawUtf8): RawUtf8;
begin
  if (StartDate <> '') and (EndDate <> '') then
    result := GermanDate(StartDate) + ' bis ' + GermanDate(EndDate)
  else if StartDate <> '' then
    result := 'ab ' + GermanDate(StartDate)
  else if EndDate <> '' then
    result := 'bis ' + GermanDate(EndDate)
  else
    result := '';
end;

// a rate as "19 %", '' when the XML has none
function Rate(const Value: RawUtf8): RawUtf8;
begin
  if Value = '' then
    result := ''
  else
    result := Decimal(Value) + ' %';
end;

// Prefix + Value, or '' when there is no value - a label never stands alone
function Labeled(const Prefix, Value: RawUtf8): RawUtf8;
begin
  if Value = '' then
    result := ''
  else
    result := Prefix + Value;
end;

{ ---------- the page ---------- }

// the item columns; 18000 = A4 (21000) minus the two 15 mm margins.
// Built at runtime: Delphi 7 has no constants for dynamic array fields
function ItemTableLayout: TTableLayout;
begin
  Finalize(result);
  FillChar(result, SizeOf(result), 0);
  SetLength(result.ColumnWidths, 5);
  result.ColumnWidths[0] := 8400;  // Bezeichnung
  result.ColumnWidths[1] := 1800;  // Menge
  result.ColumnWidths[2] := 2800;  // Einzelpreis
  result.ColumnWidths[3] := 1600;  // USt
  result.ColumnWidths[4] := 3400;  // Betrag
  SetLength(result.ColumnAligns, 5);
  result.ColumnAligns[0] := tcaLeft;
  result.ColumnAligns[1] := tcaRight;
  result.ColumnAligns[2] := tcaRight;
  result.ColumnAligns[3] := tcaRight;
  result.ColumnAligns[4] := tcaRight;
  result.HeaderFontStyle := [fsBold];
  result.HeaderBkColor := $F0E0D8;       // light blue (BGR)
  result.BodyBkColor := clWhite;
  result.AlternateRowColor := $FAF4F0;
  // the totals: bold on white, not in the header's colour
  result.FooterFontStyle := [fsBold];
  result.FooterBkColor := clWhite;
  result.GridColor := clSilver;          // quieter than the default black
end;

procedure DefineFormat(Report: TGDIPages; const Name, FontName: RawUtf8;
  Size: integer; Style: TFontStyles; Color: TColor; Before, After: integer);
var
  Fmt: TReportFormat;
begin
  Finalize(Fmt);
  FillChar(Fmt, SizeOf(Fmt), 0);
  Fmt.FontName := FontName;
  Fmt.FontSize := Size;
  Fmt.FontStyle := Style;
  Fmt.Color := Color;
  Fmt.SpaceBefore := Before;
  Fmt.SpaceAfter := After;
  Report.DefineFormat(Name, Fmt);
end;

procedure DrawInvoice(Report: TGDIPages; const Inv: TInvoice; const Sans: RawUtf8);
var
  n: integer;
  Item: TInvoiceItem;
  Name, Text, Banks, Pay: RawUtf8;
begin
  DefineFormat(Report, 'H1', Sans, 20, [fsBold], clBlack, 0, 600);
  DefineFormat(Report, 'P', Sans, 10, [], clBlack, 0, 250);
  Report.SetFont(Sans, 10);
  Report.DrawHeading(1, 'Rechnung ' + Inv.Number);
  // parties, dates and references
  Name := Inv.SellerName;
  if Inv.SellerTradingName <> '' then
    Name := Name + ' (' + Inv.SellerTradingName + ')';
  Report.DrawParagraph('Von: ' + Join([Name, Inv.SellerStreet,
    Inv.SellerPostcode + ' ' + Inv.SellerCity, Inv.SellerCountry]));
  Text := Join([Labeled('USt-IdNr. ', Inv.SellerVatId),
    Labeled('Steuernummer ', Inv.SellerTaxNumber), Inv.SellerDescription]);
  if Text <> '' then
    Report.DrawParagraph(Text);
  Text := Join([Inv.SellerContact, Labeled('Tel. ', Inv.SellerPhone),
    Inv.SellerEmail]);
  if Text <> '' then
    Report.DrawParagraph('Kontakt: ' + Text);
  Report.AddVerticalSpace(2);
  Name := Inv.BuyerName;
  if Inv.BuyerId <> '' then
    Name := Name + ' (' + Inv.BuyerId + ')';
  Report.DrawParagraph('An: ' + Join([Name, Inv.BuyerStreet,
    Inv.BuyerPostcode + ' ' + Inv.BuyerCity, Inv.BuyerCountry, Inv.BuyerEmail]));
  Report.AddVerticalSpace(2);
  Report.DrawParagraph('Rechnungsdatum: ' + GermanDate(Inv.IssueDate));
  Text := Period(Inv.PeriodStart, Inv.PeriodEnd);
  if Text <> '' then
    Report.DrawParagraph('Leistungszeitraum: ' + Text);
  if Inv.BuyerReference <> '' then
    Report.DrawParagraph('Ihre Referenz: ' + Inv.BuyerReference);
  Report.AddVerticalSpace(4);
  // the items; the table breaks the page and repeats its header on its own
  Report.BeginTable(ItemTableLayout);
  Report.DrawTableHeader(['Bezeichnung', 'Menge', 'Einzelpreis', 'USt', 'Betrag']);
  for n := 0 to high(Inv.Items) do
  begin
    Item := Inv.Items[n];
    Name := Item.Name;
    if Item.SellerId <> '' then
      Name := Name + ', Art.-Nr. ' + Item.SellerId;
    Report.DrawTableRow([Name, Decimal(Item.Quantity), Amount(Item.Price),
      Rate(Item.VatRate), Amount(Item.Total)]);
  end;
  Report.DrawTableFooter(['Summe netto', '', '', '', Amount(Inv.NetTotal)]);
  for n := 0 to high(Inv.Taxes) do
    Report.DrawTableFooter([TrimU('Umsatzsteuer ' + Rate(Inv.Taxes[n].Rate)) +
      ' auf ' + Amount(Inv.Taxes[n].Basis), '', '', '',
      Amount(Inv.Taxes[n].Amount)]);
  Report.DrawTableFooter(['Gesamtbetrag (' + Inv.Currency + ')', '', '', '',
    Amount(Inv.GrandTotal)]);
  Report.EndTable;
  Report.AddVerticalSpace(6);
  // what the items say beyond the table
  for n := 0 to high(Inv.Items) do
  begin
    Item := Inv.Items[n];
    Text := Join([Item.Description, Labeled('Klassifikation ', Item.ClassCode),
      Labeled('Abrechnungszeitraum ', Period(Item.PeriodStart, Item.PeriodEnd)),
      Labeled('Bestellposition ', Item.OrderLine)]);
    if Text <> '' then
      Text := Text + '.';
    if (Text <> '') or (Item.Note <> '') then
      Report.DrawParagraph(TrimU(Item.Name + ': ' + Text +
        Labeled(' ', Item.Note)));
  end;
  // payment and terms; each account a line of its own, so that no line break
  // falls into an IBAN
  Pay := Inv.PaymentTerms;
  if Inv.DueDate <> '' then
    Pay := TrimU(Pay + ' Zahlbar bis ' + GermanDate(Inv.DueDate) + '.');
  if Pay <> '' then
    Report.DrawParagraph(Pay);
  if Inv.PaymentReference <> '' then
    Report.DrawParagraph('Zahlungsreferenz: ' + Inv.PaymentReference);
  Banks := 'Bankverbindung: ';
  for n := 0 to high(Inv.Ibans) do
    if Inv.Ibans[n] <> '' then
    begin
      Report.DrawParagraph(Banks + Join([Inv.AccountNames[n],
        'IBAN ' + IbanGroups(Inv.Ibans[n])]));
      Banks := 'oder ';
    end;
  if Inv.Note <> '' then
    Report.DrawParagraph(Inv.Note);
  // where the data comes from
  Report.AddVerticalSpace(10);
  DefineFormat(Report, 'P', Sans, 8, [], $505050, 0, 100);
  if WithAttachment then
    Report.DrawParagraph('Die Rechnungsdaten sind als ' + XML_NAME +
      ' (ZUGFeRD / Factur-X, Profil ' + PROFILE + ') in dieses PDF eingebettet.')
  else
    Report.DrawParagraph('Ohne eingebettete Rechnungsdaten erzeugt ' +
      '(--no-attachment), die Seite ist aus ' + XML_NAME + ' gelesen.');
  Report.DrawParagraph('Beispieldaten aus XRechnung for Delphi (Landrix ' +
    'Software) - keine echte Rechnung.');
end;

{ ---------- the program ---------- }

{ <demo>_<os>_<cpu>_<compiler>.pdf next to the executable, e.g.
  zugferd_demo_windows_x64_free-pascal-3.2.2.pdf or ..._x86_delphi-7.pdf: the runs
  of all platforms and compilers can then share one folder for checking.
  OS_KIND names the distribution on Linux }
function PdfFileName: TFileName;
var
  compiler: RawUtf8;
begin
  compiler := StringReplaceAll(COMPILER_VERSION, [' 32 bit', '', ' 64 bit', '']);
  result := Executable.ProgramFilePath + Utf8ToString(LowerCase('zugferd_demo_' +
    ShortStringToAnsi7String(OS_NAME[OS_KIND]) + '_' + CPU_ARCH_TEXT + '_' +
    StringReplaceAll(compiler, ' ', '-') + '.pdf'));
end;

// factur-x.xml sits beside the .lpr; the executable is two levels below it
function LoadXml: RawUtf8;
begin
  result := StringFromFile(XML_NAME);
  if result = '' then
    result := StringFromFile(Executable.ProgramFilePath + '..' + PathDelim +
      '..' + PathDelim + XML_NAME);
end;

procedure ExportInvoice(const Inv: TInvoice; const FileName: TFileName);
var
  Report: TGDIPages;
  SansFont, SerifFont, MonoFont: RawUtf8;
  Stream: TFileStream;
begin
  Report := TGDIPages.Create(nil);
  try
    // all of this before the first NewPage - see the header
    Report.ExportPdfLevel := pdfa3U;
    Report.ExportPdfTagged := WithTags;
    Report.ExportPdfStandardFonts := false; // PDF/A embeds every font
    Report.ExportPdfEmbeddedTTF := true;
    Report.ExportPdfLanguage := 'de';
    Report.UseOutlines := true;             // PDF/UA: a bookmark per heading
    Report.GetExportFonts(SansFont, SerifFont, MonoFont);
    Report.PaperSize := psA4;
    Report.Orientation := poPortrait;
    Report.MarginLeft := 1500;
    Report.MarginRight := 1500;
    Report.MarginTop := 1500;
    Report.MarginBottom := 1500;
    Report.Title := 'Rechnung ' + Inv.Number;
    Report.Author := Inv.SellerName;
    Report.Subject := 'Rechnung mit eingebetteten ZUGFeRD / Factur-X-Daten (' +
      PROFILE + ')';
    Report.NewPage;
    DrawInvoice(Report, Inv, SansFont);
    Report.EndDoc;
    // the invoice data, under the file name ZUGFeRD and Factur-X prescribe,
    // and the XMP properties which point a reader at it
    if WithAttachment then
    begin
      Report.AddExportPdfAttachment(Xml, XML_NAME, 'Factur-X invoice data',
        'text/xml', afrAlternative);
      Report.ExportPdfMetadataExtension := PdfMetadataFacturX(PROFILE, XML_NAME);
    end;
    Stream := TFileStream.Create(FileName, fmCreate);
    try
      if not Report.ExportPdfStream(Stream) then
        raise Exception.Create('PDF export failed');
    finally
      Stream.Free;
    end;
  finally
    Report.Free;
  end;
end;

begin
  WithAttachment := true;
  WithTags := true;
  for i := 1 to ParamCount do
    if ParamStr(i) = '--no-attachment' then
      WithAttachment := false
    else if ParamStr(i) = '--untagged' then
      WithTags := false;
  Xml := LoadXml;
  if Xml = '' then
  begin
    writeln('Cannot find ', XML_NAME, ' - run from the demo folder');
    ExitCode := 1;
    exit;
  end;
  Invoice := ReadInvoice(Xml);
  ExportInvoice(Invoice, PdfFileName);
  writeln('PDF saved to ', PdfFileName);
end.
